' Gambas class file

Static Private created As Integer = 1

Static Private sprite As CSprite = New CSprite


Public Sub Quit_Click()

  Me.Close

End

Public Sub RefreshLists()
  
  Dim uuidA As Integer = 0
  Dim numAnimations As Integer = sprite.Animations.Length
  Dim numFrames As Integer
  Dim numLayers As Integer
  Dim selAnimation As String = ""
  
  If AnimationList.Selection.Length > 0 Then
    selAnimation = AnimationList.Current.Key
  Endif
  
  
  AnimationList.Clear
  FrameList.Clear
  LayerList.Clear
  
  
  For uuidA = 0 To (numAnimations - 1) 'Zero Indexed!
    AnimationList.Add(uuidA, sprite.Animations[uuidA].Name)
  Next
  
  'retain selection
  If Len(selAnimation) > 0 Then
    AnimationList.MoveTo(selAnimation)
    AnimationList.Item.Selected = True
  Endif
  
  
End


Public Sub AnimButtonAdd_Click()
  
  Dim tmpString As String = "untitled_" & CStr(created)
  Dim newAnimation As CAnimation = New CAnimation
  
  created += 1
  newAnimation.Name = tmpString
  
  If sprite.Animations.Length == 0 Then
    sprite.Animations = New CAnimation[]
  Endif
  
  sprite.Animations.Add(newAnimation)
  AnimLengthLabel.Text = sprite.Animations.Length
  
  RefreshLists

End

Public Sub AnimationList_Select()
  
  Dim currentKey As Integer = AnimationList.Current.Key + 1
  
  AnimSelectedLabel.Text = currentKey
  
  'enable top/up
  If sprite.Animations.Length > 1 And AnimationList.Selection.Length > 0 And currentKey > 1 Then
    AnimButtonTop.Enabled = True
    AnimButtonUp.Enabled = True
  Else If sprite.Animations.Length < 2 Or AnimationList.Selection.Length == 0 Or currentKey == 1 Then
    AnimButtonTop.Enabled = False
    AnimButtonUp.Enabled = False
  Endif
  
  'enable bottom/down
  If sprite.Animations.Length > 1 And AnimationList.Selection.Length > 0 And currentKey < sprite.Animations.Length Then
    AnimButtonBottom.Enabled = True
    AnimButtonDown.Enabled = True
  Else If sprite.Animations.Length < 2 Or AnimationList.Selection.Length == 0 Or currentKey == sprite.Animations.Length Then
    AnimButtonBottom.Enabled = False
    AnimButtonDown.Enabled = False
  Endif
  
  
End

Public Sub AnimationList_Rename()
  
  Dim currentKey As Integer = AnimationList.Current.Key
  
  sprite.Animations[currentKey].Name = AnimationList.Current.Text

End

Public Sub Form_Open()
  
  FMain.Resize(1280, 720)
  
  LayerSplit.Layout = [1, 4]
  FrameSplit.Layout = [1, 5]
  AnimSplit.Layout = [1, 6]
  
  Dialog.Path = "."
  
  sprite.Animations = New CAnimation[]
  

End

Public Sub Canvas_Draw()

  Dim DefaultX As Integer = 1280
  Dim DefaultY As Integer = 720
  
  Dim PadFromEdge As Integer = 10
  
  Dim ScaledX, ScaledY, PadX, PadY As Integer
  Dim Ratio, RatioX, RatioY As Float
  
  Dim CenterX As Integer = Canvas.W / 2
  Dim CenterY As Integer = Canvas.H / 2
  
  Dim Dashes As Float[] = [1.0, 1.0]
  
  If Canvas.W / DefaultX >= Canvas.H / DefaultY Then
    Ratio = Canvas.H / DefaultY * 0.9
  Else
    Ratio = Canvas.W / DefaultX * 0.9
  Endif
  
  Paint.Reset
  
  Paint.LineWidth = 1
  Paint.Brush = Paint.Color(Color.Black)
  Paint.Rectangle(CenterX - (DefaultX / 2 * Ratio), CenterY - (DefaultY / 2 * Ratio), DefaultX * Ratio, DefaultY * Ratio)
  Paint.Stroke
  
  Paint.Dash = Dashes
  Paint.Brush = Paint.Color(Color.White)
  Paint.Rectangle(CenterX - (DefaultX / 2 * Ratio), CenterY - (DefaultY / 2 * Ratio), DefaultX * Ratio, DefaultY * Ratio)
  Paint.Stroke

End

Public Sub LayerButtonImage_Click()
  
  Dialog.Filter = ["*.png;*.jpg;*.jpeg;*.bmp", "Picture files"]
  If Dialog.OpenFile() Then
    Print "cancel"
  Else
    Print Dialog.Path
  Endif

End

Public Sub AnimationList_DblClick()

  AnimationList.Current.Rename

End

Public Sub AnimButtonTop_Click()

  Dim currentKey As Integer = AnimationList.Current.Key
  
  sprite.Animations.Add(sprite.Animations[currentKey], 0)
  sprite.Animations.Remove(currentKey + 1)
  
  RefreshLists()
  
  AnimationList.MoveTo("0")
  AnimationList.Item.Selected = True
  
End


Public Sub AnimButtonUp_Click()

  Dim currentKey As Integer = AnimationList.Current.Key
  
  sprite.Animations.Add(sprite.Animations[currentKey], currentKey - 1)
  sprite.Animations.Remove(currentKey + 1)
  
  RefreshLists()
  
  AnimationList.MoveTo(currentKey - 1)
  AnimationList.Item.Selected = True

End

Public Sub AnimButtonBottom_Click()
  
  Dim currentKey As Integer = AnimationList.Current.Key
  
  sprite.Animations.Add(sprite.Animations[currentKey])
  sprite.Animations.Remove(currentKey)
  
  RefreshLists()
  
  AnimationList.MoveTo(sprite.Animations.Length - 1)
  AnimationList.Item.Selected = True

End

Public Sub AnimButtonDown_Click()

  Dim currentKey As Integer = AnimationList.Current.Key
  
  sprite.Animations.Add(sprite.Animations[currentKey], currentKey + 2)
  sprite.Animations.Remove(currentKey)
  
  RefreshLists()
  
  AnimationList.MoveTo(currentKey + 1)
  AnimationList.Item.Selected = True
  
End
